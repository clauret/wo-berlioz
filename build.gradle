plugins {
  id 'java-library'
  id 'maven-publish'
  id 'io.codearte.nexus-staging' version '0.30.0'
  id 'com.github.ben-manes.versions' version '0.49.0'
}

ext {
  globalVersion = file('version.txt').text.trim()
}

allprojects {
  group   = 'org.pageseeder.berlioz'
  version = "$globalVersion"

  apply plugin: 'java-library'
  apply from: "$rootDir/gradle/publish-mavencentral.gradle"

  java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  repositories {
    mavenCentral {
      url = uri("https://maven-central.storage.googleapis.com/maven2")
    }
  }

  jar {
    manifest {
      attributes 'Implementation-Vendor':  'Allette Systems',
                 'Implementation-Title':   "$title",
                 'Implementation-Version': "$globalVersion"
    }
  }

  javadoc {

    // Let's package the javadoc even if there are some lint issues
    options.addStringOption('Xdoclint:none', '-quiet')

    // Generate better doc if we can
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }

}

dependencies {

  api('org.pageseeder.xmlwriter:pso-xmlwriter:1.0.4')

  implementation('org.slf4j:slf4j-api:2.0.9')

  compileOnly('jakarta.servlet:jakarta.servlet-api:4.0.4'){
    because 'This is provided by the Servlet container'
  }

  // NB Only one of the JSON libs is required
  compileOnly('com.fasterxml.jackson.core:jackson-core:2.15.3') {
    because 'This is an optional dependencies for JSON output using Jackson'
  }
  compileOnly('com.google.code.gson:gson:2.10.1') {
    because 'This is an optional dependencies for JSON output using Google JSON library'
  }
  compileOnly('javax.json:javax.json-api:1.1.4') {
    because 'These is an optional dependencies for JSON output using implementations of JSR 374'
  }
  compileOnly('jakarta.json:jakarta.json-api:2.1.3') {
    because 'These is an optional dependencies for JSON output using implementations of JSR 374'
  }

  compileOnly('org.eclipse.jdt:org.eclipse.jdt.annotation:2.2.700') {
    because 'This used for Null safety and better interop with Kotlin'
  }

  testImplementation('junit:junit:4.13.2')
  testImplementation('org.slf4j:slf4j-simple:2.0.9')
  testImplementation('org.glassfish:javax.json:1.1.4')
  testImplementation('org.glassfish:jakarta.json:2.0.1')
  testImplementation('com.fasterxml.jackson.core:jackson-core:2.15.0')
  testImplementation('com.google.code.gson:gson:2.10.1')

}

wrapper {
  gradleVersion = '7.6'
  distributionType = Wrapper.DistributionType.ALL
}

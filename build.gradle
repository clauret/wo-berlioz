plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
}

ext {
  globalVersion = file('version.txt').text
}

allprojects {
  group   = 'org.pageseeder.berlioz'
  version = "$globalVersion"

  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.bintray'
  apply from: "$rootDir/gradle/publishing.gradle"

  java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  repositories {
    maven { url 'https://dl.bintray.com/pageseeder/maven' }
    jcenter()
  }

  jar {
    manifest {
      attributes 'Implementation-Vendor':  'Allette Systems',
                 'Implementation-Title':   "$title",
                 'Implementation-Version': "$globalVersion"
    }
  }

  javadoc {

    // Let's package the javadoc even if there are some lint issues
    options.addStringOption('Xdoclint:none', '-quiet')

    // Generate better doc if we can
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }

}

dependencies {

  api('org.pageseeder.xmlwriter:pso-xmlwriter:1.0.2')

  implementation('org.slf4j:slf4j-api:1.7.30')

  compileOnly('javax.servlet:javax.servlet-api:3.1.0'){
    because 'This is provided by the Servlet container'
  }

  // NB Only one of the JSON libs is required
  compileOnly('com.fasterxml.jackson.core:jackson-core:2.10.1') {
    because 'This is an optional dependencies for JSON output using Jackson'
  }
  compileOnly('com.google.code.gson:gson:2.8.6') {
    because 'This is an optional dependencies for JSON output using Google JSON library'
  }
  compileOnly('javax.json:javax.json-api:1.1.4') {
    because 'These is an optional dependencies for JSON output using implementations of JSR 374'
  }

  compileOnly('org.eclipse.jdt:org.eclipse.jdt.annotation:2.0.0') {
    because 'This used for Null safety and better interop with Kotlin'
  }

  testImplementation('junit:junit:4.12')
  testImplementation('org.slf4j:slf4j-simple:1.7.30')
  testImplementation('org.glassfish:javax.json:1.1.4')
  testImplementation('com.fasterxml.jackson.core:jackson-core:2.10.1')
  testImplementation('com.google.code.gson:gson:2.8.6')

}

wrapper {
  gradleVersion = '6.1'
  distributionType = Wrapper.DistributionType.ALL
}

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
}

ext {
  globalVersion = file('version.txt').text
}

allprojects {
  group   = 'org.pageseeder.berlioz'
  version = "$globalVersion"

  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.bintray'
  apply from: "$rootDir/gradle/publishing.gradle"

  java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  repositories {
    maven { url 'https://dl.bintray.com/pageseeder/maven' }
    jcenter()
  }

  jar {
    manifest {
      attributes 'Implementation-Vendor':  'Allette Systems',
                 'Implementation-Title':   "$title",
                 'Implementation-Version': "$globalVersion"
    }
  }

  javadoc {

    // Let's package the javadoc even if there are some lint issues
    options.addStringOption('Xdoclint:none', '-quiet')

    // Generate better doc if we can
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }

}

description = "Berlioz micro-Web framework"

dependencies {

  api('org.pageseeder.xmlwriter:pso-xmlwriter:1.0.2')

  implementation('org.slf4j:slf4j-api:1.7.30')

  // Expected to be provided by environment
  compileOnly (
      'javax.servlet:javax.servlet-api:3.1.0',
      'com.fasterxml.jackson.core:jackson-core:2.8.9',  // Jackson (JSON)
      'com.google.code.gson:gson:2.8.1',                // Google JSON library
      'javax.json:javax.json-api:1.0',                  // Oracle JSONP (JSR-353)
      'org.eclipse.jdt:org.eclipse.jdt.annotation:2.0.0'
  )

  testImplementation('junit:junit:4.12')
  testImplementation('org.slf4j:slf4j-simple:1.7.30')
  testImplementation('org.glassfish:javax.json:1.0.4')

}

wrapper {
  gradleVersion = '6.1'
  distributionType = Wrapper.DistributionType.ALL
}
